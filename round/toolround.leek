include("cellround");
/******************************************************************************
 * ToolRound
 *
 * Exist only if cooldown == 0
 ******************************************************************************/
class ToolRound extends Objects {
  
  Tool tool;
  integer cooldown; 
  
  LeekRound owner;
  
  /**
   * constructor
   **/
  constructor(Tool|ToolRound tool, LeekRound owner) {
	this.owner = owner;
    id = tool.id;
    if(tool.class.super.name == "Tool") {
      this.tool = tool as Tool;
      cooldown = 0;
    } else {
      this.tool = (tool as ToolRound).tool;
      cooldown = tool.cooldown;
    }
  }

  /**
   * simulateUse
   * @return {integer minEffect, integer maxEffect, integer midEffect}
   **/
  Object simulateUse(Array<LeekRound> targets = [this.owner], Array<real> AOEs = [1.0]) {
    integer cost = tool.cost;
    owner.minTP -= cost;
    owner.maxTP -= cost;
    owner.midTP -= cost;
    cooldown = tool.cooldown;
    Object delta = {minEffect:0,maxEffect:0,midEffect:0};
    real midCriticalLuck = owner.midAgility/1000.0;
	real criticalFactor = CRITICAL_FACTOR;
	if(owner.maxAgility <= 0) criticalFactor = 1.0;
    for(Effect effect in tool.effects) {
      if(effect.modOnCast) {
		GameDebug.add(EFFECT_DEBUG,"apply on caster only:");
        Object delta2 = effect.simulateUse(owner, owner, 1.0, 1, midCriticalLuck, criticalFactor)
        delta.minEffect += delta2.minEffect;
        delta.maxEffect += delta2.maxEffect;
        delta.midEffect += delta2.midEffect;
      } else {
		integer nbTargets = count(targets);
        for(integer i : LeekRound target in targets) {
          Object delta2 = effect.simulateUse(owner, target, AOEs[i], nbTargets, midCriticalLuck, criticalFactor)
          delta.minEffect += delta2.minEffect;
          delta.maxEffect += delta2.maxEffect;
          delta.midEffect += delta2.midEffect;
        }
      }
    }
    return delta;
  }
  
  /**
   * toString
   *
   * @return string
   **/
  string shortString() {
    return this.class.name+"("+TOOL_NAME_TRANSLATOR[id]+")";
  }
  
}

/******************************************************************************
 * WeaponRound
 *
 * Exist only if cooldown == 0
 ******************************************************************************/
class WeaponRound extends ToolRound {
  
  /**
   * constructor
   **/
  constructor(Tool|ToolRound tool, LeekRound owner) {
    super(tool, owner);
  }
  
}

/******************************************************************************
 * ChipRound
 *
 * Exist only if cooldown == 0
 ******************************************************************************/
class ChipRound extends ToolRound {
  
  /**
   * constructor
   **/
  constructor(Tool|ToolRound tool, LeekRound owner) {
    super(tool, owner);
  }
  
}
