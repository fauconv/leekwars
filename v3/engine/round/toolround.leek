/******************************************************************************
 * ToolRound
 *
 * Exist only if cooldown == 0
 ******************************************************************************/
class ToolRound extends Objects {
  
  Tool tool;
  integer cooldown; 
  Round roundObj;
  LeekRound owner;
  
  /**
   * constructor
   **/
  constructor(Tool|ToolRound tool, LeekRound owner) {
    this.owner = owner;
    roundObj = owner.roundObj;
    id = tool.id;
    if(tool.class.super.name == "Tool") {
      this.tool = tool as Tool;
      cooldown = 0;
    } else {
      this.tool = (tool as ToolRound).tool;
      cooldown = tool.cooldown;
    }
  }
  
  /**
   * simulateUseOnCell
   * abstract
   **/
  EffectResult simulateUseOnCell(integer cellId) {
    return new EffectResult();
  }

  /**
   * simulateUse
   * @return {integer minEffect, integer maxEffect, integer midEffect}
   **/
  EffectResult simulateUse(Array<Integer> targets = [this.owner.id], Array<real> AOEs = [1.0]) {
    integer cost = tool.cost;
    owner.minTP -= cost;
    owner.maxTP -= cost;
    owner.midTP -= cost;
    if(owner.minTP < 0) debugC("ToolRound=>simulateUse=>ERREUR !!!!",COLOR_RED);
    cooldown = tool.cooldown;
    EffectResult delta = new EffectResult();
    real midCriticalLuck = owner.midAgility/1000.0;
    real criticalFactor = CRITICAL_FACTOR;
    (owner.maxAgility <= 0) and (criticalFactor = 1.0);
    for(Effect effect in tool.effects) {
      if(effect.modOnCast) {
        GameDebug.add(EFFECT_DEBUG,"apply on caster only:");
        EffectResult delta2 = effect.simulateUse(owner, owner, 1.0, 1, midCriticalLuck, criticalFactor)
        GameDebug.add(EFFECT_DEBUG, delta2,"effect dmg");
        delta.add(delta2);
      } else {
        integer nbTargets = count(targets);
        for(integer i : integer targetId in targets) {
          LeekRound target = roundObj.leeksRound[targetId]!;
          EffectResult delta2 = effect.simulateUse(owner, target, AOEs[i], nbTargets, midCriticalLuck, criticalFactor)
          GameDebug.add(EFFECT_DEBUG, delta2,"effect dmg");
          delta.add(delta2);
        }
      }
    }
    return delta;
  }
  
  /**
   * UseOnCell
   * abstract
   **/
  void useOnCell(integer cellId) {
  }

  /**
   * toString
   *
   * @return string
   **/
  string shortString(string space = "") {
    return this.class.name+"("+TOOL_NAME_TRANSLATOR[id]+")";
  }
  
}

/******************************************************************************
 * WeaponRound
 *
 * Exist only if cooldown == 0
 ******************************************************************************/
class WeaponRound extends ToolRound {
  
  /**
   * constructor
   **/
  constructor(Tool|ToolRound tool, LeekRound owner) {
    super(tool, owner);
  }
  
  /**
   * useOnCell
   * attention il ne degaine pas
   * Attention il faut faire un update du round apres
   **/
  void useOnCell(integer cellId) {
    useWeaponOnCell(cellId);
  }

  /**
   * simulateUseOnCell
   **/
  EffectResult simulateUseOnCell(integer cellId) {
    Array<integer> entities = getWeaponTargets(id, cellId)!;
    integer nbTarget = count(entities);
    Array<real> AOEs = [];
    for(integer entity in entities) {
      push(AOEs, max(0.0,1.0-
                     roundObj.leeksRound[entity]!.currentCell.cell.interCellInfo[cellId].range*AOE_DEC_LINE_FACTOR));
      
    }
    return simulateUse(entities, AOEs);
  }
    
}

/******************************************************************************
 * ChipRound
 *
 * Exist only if cooldown == 0
 ******************************************************************************/
class ChipRound extends ToolRound {
  
  /**
   * constructor
   **/
  constructor(Tool|ToolRound tool, LeekRound owner) {
    super(tool, owner);
  }
  
  /**
   * useOnCell
   * Attention il faut faire un update du round apres
   **/
  void useOnCell(integer cellId) {
    useChipOnCell(id, cellId);
  }
  
  /**
   * simulateUseOnCell
   * abstract
   **/
  EffectResult simulateUseOnCell(integer cellId) {
    Array<integer> entities = getChipTargets(id, cellId)!;
    integer nbTarget = count(entities);
    Array<real> AOEs = [];
    for(integer entity in entities) {
      push(AOEs, max(0.0,1.0-roundObj.leeksRound[entity]!.currentCell.cell.interCellInfo[cellId].range*0.2));
      
    }
    return simulateUse(entities, AOEs);
  }
    
}
